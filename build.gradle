plugins {
    id 'eclipse'
    id 'idea'
    id 'fabric-loom' version '1.11-SNAPSHOT'
}

version = project.mod_version
group = project.maven_group
archivesBaseName = "kaleidoscopecookery"

loom {
    accessWidenerPath = file("src/main/resources/kaleidoscope_cookery.accesswidener")
}

fabricApi {
    configureDataGeneration()
}

repositories {
    maven { url "https://maven.shedaniel.me"}
    exclusiveContent {
        forRepository {
            maven {
                url "https://cursemaven.com/"
            }
        }
        filter {
            includeGroup "curse.maven"
        }
    }
    exclusiveContent {
        forRepository {
            maven {
                name = "Modrinth"
                url = "https://api.modrinth.com/maven"
            }
        }
        filter {
            includeGroup "maven.modrinth"
        }
    }
    maven {
        name = 'ParchmentMC'
        url = 'https://maven.parchmentmc.org'
    }
    maven {
        // location of the maven that hosts JEI files since January 2023
        // Patchouli
        name = "Jared's maven"
        url = "https://maven.blamejared.com/"
    }
    maven {
        // location of a maven mirror for JEI files, as a fallback
        name = "ModMaven"
        url = "https://modmaven.dev"
    }
    maven {
        name = "Greenhouse Maven"
        url = 'https://maven.greenhouse.lgbt/releases/'
    }
    maven {
        url "https://jitpack.io/" // Fabric ASM
        content {
            excludeGroup "io.github.fabricators_of_create"
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${project.minecraft_version}:${project.parchment_version}@zip")
    }
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    modImplementation("vectorwing:FarmersDelight:${fdrf_version}") {
        exclude(group: "net.fabricmc")
    }
    modImplementation "curse.maven:ftb-ultimine-fabric-448231:7188393"
    modImplementation "curse.maven:ftb-library-fabric-438495:7260615"
    modImplementation("mezz.jei:jei-${project.minecraft_version}-fabric:${project.jei_version}")
    modImplementation "curse.maven:jade-324717:6738760"
    modImplementation "curse.maven:roughly-enough-items-310111:6199139"
    modImplementation "curse.maven:cloth-config-348521:5729125"
    modApi("me.shedaniel.cloth:basic-math:+")
    modImplementation "curse.maven:architectury-api-419699:5786326"
}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "loader_version", project.loader_version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand(
                "version": project.version,
                "minecraft_version": project.minecraft_version,
                "loader_version": project.loader_version
        )
    }
}

def targetJavaVersion = 21
tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release.set(targetJavaVersion)
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    withSourcesJar()
}